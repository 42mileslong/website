---
layout:	analytics
---

<head>
    <meta charset="utf-8">
    <meta http-equiv="X-UA-Compatible" content="IE=edge,chrome=1">

    <title>Rafi Long</title>
</head>

<script id="shader-vs" type="x-shader/x-vertex">
    attribute vec3 aVertexNormal;
    attribute vec3 aVertexPosition;

    uniform bool uColorHeight;
    uniform mat4 uMVMatrix;
    uniform mat4 uPMatrix;
    uniform mat3 uNMatrix;

    varying vec3 vPosition;
    varying vec3 vNormal;
    varying vec3 vColorDiffuse;

    void main(void) {
      vec4 vertexPositionEye4 = uMVMatrix * vec4(aVertexPosition, 1.0);
      vPosition = vertexPositionEye4.xyz / vertexPositionEye4.w;
      vNormal = normalize(uNMatrix * aVertexNormal);

      // Color choices from https://www.colourlovers.com
      if (uColorHeight) {
        if (aVertexPosition.z > 0.05) {
          vColorDiffuse = vec3(1, 1, 1);
        } else if (aVertexPosition.z > -0.0) {
          vColorDiffuse = vec3(103.0/255.0,89.0/255.0,106.0/255.0);
        } else if (aVertexPosition.z > -0.05) {
          vColorDiffuse = vec3(136.0/255.0,159.0/255.0,87.0/255.0);
        } else {
          vColorDiffuse = vec3(11.0/255.0,72.0/255.0,107.0/255.0);
        }
      }

      gl_Position = uPMatrix * uMVMatrix * vec4(aVertexPosition, 1.0);
    }
</script>

<script id="shader-fs" type="x-shader/x-fragment">
    precision mediump float;

    uniform vec3 uLightPosition;
    uniform vec3 uAmbientLightColor;
    uniform vec3 uDiffuseLightColor;
    uniform vec3 uSpecularLightColor;

    uniform bool uColorHeight;
    uniform vec3 uKAmbient;
    uniform vec3 uKDiffuse;
    uniform vec3 uKSpecular;
    uniform float uShininess;

    varying vec3 vPosition;
    varying vec3 vNormal;
    varying vec3 vColorDiffuse;

    uniform bool uFogToggle;

    void main(void) {
      vec3 vectorToLightSource = normalize(uLightPosition - vPosition);
      vec3 normalEye = normalize(vNormal);

      vec3 viewVectorEye = -normalize(vPosition);
      vec3 halfViewEyeLight = normalize(viewVectorEye + vectorToLightSource);
      float hdotn = max(dot(halfViewEyeLight, normalEye), 0.0);

      float specularLightWeightning = pow(hdotn, uShininess);
      float diffuseLightWeightning = max(dot(normalEye, vectorToLightSource), 0.0);

      vec4 fragColor = vec4(((uAmbientLightColor*uKAmbient)
                      + (uDiffuseLightColor*(uColorHeight ? vColorDiffuse : uKDiffuse)) * diffuseLightWeightning
                      + ((uSpecularLightColor*uKSpecular) * specularLightWeightning)),1.0);

      float fogCoord = (gl_FragCoord.z / gl_FragCoord.w);
      vec4 fogColor = vec4(1.0, 1.0, 1.0, 1.0);

      if (uFogToggle) {
        const float LOG2 = 1.442695;
        float fogDensity = 0.4;
        float fogFactor = clamp(exp2( -fogDensity * fogDensity * fogCoord * fogCoord * LOG2), 0.0, 1.0);

        gl_FragColor = mix(fogColor, fragColor, fogFactor);
      } else {
        gl_FragColor = fragColor;
      }
    }
</script>

<script src="/assets/js/gl-matrix-min.js"></script>
<script src="/assets/js/webgl-utils.js"></script>
<script src="/assets/js/Terrain.js"></script>
<script src="/assets/js/background.js"></script>
<body onload="startup();">
 <canvas id="myGLCanvas" width="800" height="800"></canvas>
  {{ content }}
</body>
